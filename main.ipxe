#!ipxe

echo It works!

:start

chain --autofree boot.cfg ||

iseq ${cls} serial && goto ignore_cls ||
set cls:hex 1b:5b:4a  # ANSI clear screen sequence - "^[[J"
set cls ${cls:string}
:ignore_cls

isset ${arch} && goto skip_arch_detect ||
cpuid --ext 29 && set arch x86_64 || set arch i386
:skip_arch_detect
isset ${menu} && goto ${menu} ||

isset ${ip} || dhcp || echo DHCP failed

## MAIN MENU ##

:menu
clear menu
set space:hex 20:20
set space ${space:string}
iseq ${arch} i386 && set arch5 i586 || set arch5 ${arch}
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
menu ${boot_domain} [Local IP: ${netX/ip}]

item --gap Default:
item local ${space} Boot from local hdd

item --gap Operating Systems:
item linux ${space} Linux Operating Systems
item bsd ${space} BSD Operating Systems
item experimental ${space} Experimental Operating Systems

item --gap Tools:
item utils ${space} Utilities
item shell ${space} iPXE shell
item netinfo ${space} Network card info

isset ${menu} && set timeout 0 || set timeout 300000
choose --timeout ${timeout} --default ${menu} menu || goto local
echo ${cls}
goto ${menu} ||
chain ${menu}.ipxe || goto failed
goto menu

:cancel
echo You cancelled the menu, dropping you to a shell
goto shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto menu

:failed
echo Error occured, press any key to return to menu ...
prompt
goto menu

:local
echo Booting from local disks ...
exit 0

:minixp
initrd /images/mxp14.iso
chain /memdisk iso raw || goto failed

:winpe
initrd http://192.168.1.3/images/winpe.iso
chain memdisk iso raw || goto failed

:netboot
chain http://boot.netboot.xyz  || goto failed
goto menu

:bfo
chain http://dl.fedoraproject.org/pub/alt/bfo/ipxe.iso || goto failed
goto menu

:erpxe
chain https://raw.githubusercontent.com/ERPXE/tftpboot/master/undionly.kpxe || goto failed
goto menu

:netinfo
menu Network info
item --gap MAC:
item mac ${space} ${netX/mac}
item --gap IP/mask:
item ip ${space} ${netX/ip}/${netX/netmask}
item --gap Gateway:
item gw ${space} ${netX/gateway}
item --gap Domain:
item domain ${space} ${netX/domain}
item --gap DNS:
item dns ${space} ${netX/dns}
item --gap DHCP server:
item dhcpserver ${space} ${netX/dhcp-server}
item --gap Next-server:
item nextserver ${space} ${next-server}
item --gap Filename:
item filename ${space} ${netX/filename}
choose empty ||
goto main_menu

:shutdown
shutdown

:reboot
reboot

:exit
exit

:config
config
goto menu

:back
set submenu-timeout 0
clear submenu-default
goto menu

:reload
:reload
echo Reloading boot.ipxe ...
chain --autofree boot.ipxe ||
goto reload
